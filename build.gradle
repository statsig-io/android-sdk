buildscript {
    ext.kotlin_version = '1.5.0'
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.5.1"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'signing'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName project.getProperty('libraryVersion')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            debuggable true
        }
        release {
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'consumer-rules.pro'
        }
    }

    testOptions {
        kotlinOptions {
            freeCompilerArgs += [
                    '-Xopt-in=kotlin.RequiresOptIn',
            ]
        }
    }
}

ktlint {
    version = "0.50.0"
    verbose = true
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

task testLogging(type: Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
    archiveClassifier.set('sources')
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
    testImplementation "org.slf4j:slf4j-simple:2.0.17"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testImplementation 'com.google.truth:truth:1.4.5'
    testImplementation 'org.robolectric:robolectric:4.10'
}

artifacts {
    archives sourceJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group 'com.statsig'
                artifactId 'android-sdk'
                version project.getProperty('libraryVersion')

                artifact tasks.sourcesJar

                pom {
                    name = "Statsig Android SDK"
                    description = "A SDK for integrating Statsig with Android Apps"
                    url = "https://github.com/statsig-io/android-sdk"
                    organization {
                        name = "Statsig"
                        url = "https://www.statsig.com"
                    }
                    licenses {
                        license {
                            name = "Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = "tore"
                            name = "Tore Hanssen"
                            email = "tore@statsig.com"
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/statsig-io/android-sdk.git")
                        developerConnection.set("scm:git:ssh://git@github.com:statsig-io/android-sdk.git")
                        url.set("https://github.com/statsig-io/android-sdk")
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = System.getenv("ORG_GRADLE_PROJECT_MAVEN_USERNAME")
                    password = System.getenv("ORG_GRADLE_PROJECT_MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        def signingKeyId = System.getenv("ORG_GRADLE_PROJECT_SIGNING_KEY_ID") ?: ""
        def signingKey = System.getenv("ORG_GRADLE_PROJECT_SIGNING_KEY") ?: ""
        def signingPassword = System.getenv("ORG_GRADLE_PROJECT_SIGNING_PASSWORD") ?: ""
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications['release']
    }
}

if (project == rootProject) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
                snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
                username = System.getenv("ORG_GRADLE_PROJECT_MAVEN_USERNAME")
                password = System.getenv("ORG_GRADLE_PROJECT_MAVEN_PASSWORD")
            }
        }
    }
}


